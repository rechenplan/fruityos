;  usage: fill file n
;  Write n zeros to a file

sub fr_mul(a, b)
        local r
        while b > 0 do
                r = r + a
                b = b - 1
        end
        return r
end

sub atoi(str)
        local n, d, x
        while n < strlen(str) do
                x = fr_mul(x, 10)
                d = byte [str + n] - byte ["0"]
                x = x + d
                n = n + 1
        end
        return x
end

sub strlen(str)
	local n
	while byte [str] != 0 do
		str = str + 1
		n = n + 1
	end
	return n
end

sub puts(str)
	while byte [str] != 0 do
		putch(byte [str])
		str = str + 1
	end
end


sub nextarg(strPtr)
	local c
	while c = byte [strPtr] do
		strPtr = strPtr + 1
	end
	return strPtr + 1
end

sub main(argc, argv)
	local fd, n, buf
	if argc != 3 then
		puts("usage: fill file n")
		putch(10)
		return 0
	end
	buf = brk(-1)
	brk(buf + 1)

	argv = nextarg(argv)
	fd = creat(argv)
	if fd == 0 then
		puts("Unable to open file")
	end
	argv = nextarg(argv)
	n = atoi(argv)

	while n > 0 do
		byte [buf] = 0
		write(fd, buf, 1)
		n = n - 1
	end
	close(fd)
end
