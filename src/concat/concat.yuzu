
{

  usage: concat outfile [ infile ]

  writes a series of files in order to an output file

}

sub malloc(size)
	local ptr
	ptr = brk(-1)
	brk(ptr + size)
	return ptr
end

sub puts(strPtr)
	local c
	while c = [strPtr] & 255 do
		putch(c)
		strPtr = strPtr + 1
	end
	return 0
end

sub nextarg(strPtr)
	local c
	while c = [strPtr] & 255 do
		strPtr = strPtr + 1
	end
	return strPtr + 1
end

sub main(argc, argv)
	local fd, fout, bufPtr
	if argc < 2 then
		puts("usage: concat outfile { infile }")
		putch(10)
		exit()
	end
	bufPtr = malloc(1)
	argv = nextarg(argv)
	fout = creat(argv)
	while argc > 2 do
		argv = nextarg(argv)
		fd = open(argv, 0)
		if fd > 0 then
			while read(fd, bufPtr, 1) > 0 do
				fputch(fout, [bufPtr] & 255)
			end
			close(fd)
		end
		argc = argc - 1
	end
	close(fout)
	return 0
end
