; page table

sub page_init()
	local idx, addr

	idx = 0
	addr = 7
	; Identity map first 512 MB
	while idx < 131072 do ; 512 * 256
		page_setPTEntry(idx, addr)
		idx = idx + 1
		addr = addr + 4096
	end

	kmemset(page_getPDAddress(), 4096, 0)
	idx = 0
	addr = page_getPTAddress() | 7
	while idx < 256 do
		page_setPDEntry(idx, addr)
		idx = idx + 1
		addr = addr + 4096
	end

	kmemset(page_getPDPAddress(), 4096, 0)
	page_setPDPEntry(0, page_getPDAddress() | 7)
	kmemset(page_getPML4Address(), 4096, 0)
	page_setPML4Entry(0, page_getPDPAddress() | 7)

	flush_tlb(page_getBaseAddress())
end

sub page_swapProcess(pid)
	local idx, addr, i
	
	idx = (pid + 1) << 2
	addr = (page_getPTAddress() + ((pid + 1) << 12)) | 7
	i = 0

	while i < 4 do
		page_setPDEntry(idx + i, addr)
		addr = addr + 4096
		i = i + 1
	end
	flush_tlb(page_getBaseAddress())
end


sub page_setPML4Entry(idx, entry)
	return page_setEntry(page_getPML4Address(), idx, entry)
end

sub page_setPDPEntry(idx, entry)
	return page_setEntry(page_getPDPAddress(), idx, entry)
end

sub page_setPDEntry(idx, entry)
	return page_setEntry(page_getPDAddress(), idx, entry)
end

sub page_setPTEntry(idx, entry)
	return page_setEntry(page_getPTAddress(), idx, entry)
end

sub page_setEntry(addr, idx, entry)
	[addr + (idx << 3)] = entry
end

sub page_cleanup()
	return 0
end

sub page_getSize()
	return 0
end

sub page_getPML4Address()
	return page_getBaseAddress() + (0 << 12)
end

sub page_getPDPAddress()
	return page_getBaseAddress() + (1 << 12)
end

sub page_getPDAddress()
	return page_getBaseAddress() + (2 << 12)
end

sub page_getPTAddress()
	return page_getBaseAddress() + (3 << 12)
end

sub page_getBaseAddress()
	return 1 << 20
end
